apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.android.gms.oss-licenses-plugin'

apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion rootProject.ext.compile_sdk_version

    defaultConfig {
        applicationId "com.pr656d.cattlenotes"
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        // Add this debug config to provide debug settings
        debug {

        }
        release {
            minifyEnabled true  // To enable proguard
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {

    // JARs
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support Library
    implementation "androidx.appcompat:appcompat:$rootProject.app_compat_version"

    // Kotlin
    implementation "androidx.core:core-ktx:$rootProject.core_ktx_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin_version"

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.coroutine_core_version"

    // Constraint Layout
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraint_version"

    // Material Design
    implementation "com.google.android.material:material:$rootProject.material_version"

    // Navigation controller
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.nav_version"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:$rootProject.support_library_version"

    // CardView
    implementation "androidx.cardview:cardview:$rootProject.support_library_version"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycle_version"

    // Lifecycle Annotations
    kapt "androidx.lifecycle:lifecycle-common-java8:$rootProject.lifecycle_version"

    // Multidex
    implementation "androidx.multidex:multidex:$rootProject.multidex_version"

    // Dagger
    implementation "com.google.dagger:dagger-android:$rootProject.dagger_version"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger_version"
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger_version"

    // Room
    implementation "androidx.room:room-runtime:$rootProject.room_version"
    implementation "androidx.room:room-ktx:$rootProject.room_version"
    kapt "androidx.room:room-compiler:$rootProject.room_version"

    // Image
    implementation "com.github.bumptech.glide:glide:$rootProject.glide_version"
    kapt "com.github.bumptech.glide:compiler:$rootProject.glide_version"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttp_version"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$rootProject.retrofit2_rxjava2_adapter"

    // logger
    implementation "com.jakewharton.timber:timber:$rootProject.timber_version"

    // Parser
    implementation "com.google.code.gson:gson:$rootProject.gson_version"

    // Debugging
    debugImplementation "com.amitshekhar.android:debug-db:$rootProject.debug_db_version"

    // Open-source licences
    implementation "com.google.android.gms:play-services-oss-licenses:$rootProject.open_source_licences_version"

    // Local Unit tests
    testImplementation "junit:junit:$rootProject.junit_version"
    testImplementation "org.mockito:mockito-core:$rootProject.mockito_version"
    kaptTest "com.google.dagger:dagger-compiler:$rootProject.dagger_version"
    testImplementation "android.arch.core:core-testing:$rootProject.core_testing_version"
    testImplementation "org.hamcrest:hamcrest-library:$rootProject.hamcrest_version"

    // UI Testing
    androidTestImplementation "androidx.test:runner:$rootProject.test_runner_version"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.test_ext_runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.espresso_version"
    androidTestImplementation "org.mockito:mockito-core:$rootProject.mockito_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$rootProject.dagger_version"

    // Para camera
    implementation "com.mindorks:paracamera:$rootProject.paracamera_version"

    // Firebase
    implementation "com.google.firebase:firebase-auth:$rootProject.firebase_auth_version"
    implementation "com.firebaseui:firebase-ui-auth:$rootProject.firebase_uiauth_version"
    implementation "com.google.firebase:firebase-analytics:$rootProject.firebase_analytics_version"
    implementation "com.google.firebase:firebase-database:$rootProject.firebase_database_version"
    // Required only if Facebook login support is required
    // Find the latest Facebook SDK releases here: https://goo.gl/Ce5L94
    implementation "com.facebook.android:facebook-android-sdk:$rootProject.facebook_sdk_version"
}

apply plugin: 'com.google.gms.google-services'